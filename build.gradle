plugins {
  alias(libs.plugins.indra)
  alias(libs.plugins.indra.publishing.sonatype)
  alias(libs.plugins.spotless)
}

group = "net.kyori"
version = "1.0.0-SNAPSHOT"
description = "A library to render Minecraft chat component styles to ANSI control codes"

repositories {
    mavenCentral()
}

dependencies {
    compileOnlyApi libs.jetbrainsAnnotations
    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.api
    testRuntimeOnly libs.junit.engine
}

spotless {
  java {
    def lineSep = System.lineSeparator()
    File contents = rootProject.file('license_header.txt')
    def licenseText = "/*" + lineSep + contents.readLines('utf-8')
      .collect { (" * " + it).stripTrailing() }
      .join(lineSep) + lineSep + " */"

    licenseHeader(licenseText)
  }
}

sourceSets {
    main {
        multirelease {
            alternateVersions(9)
            moduleName "net.kyori.ansi"
        }
    }
}

indraSonatype {
  useAlternateSonatypeOSSHost("s01")
}

indra {
    github("KyoriPowered", "ansi") {
        ci(true)
    }
    mitLicense()

    javaVersions {
        testWith(8, 11, 18)
    }

    configurePublications {
        pom {
            developers {
                developer {
                    id = "kashike"
                    timezone = "America/Vancouver"
                }

                developer {
                    id = "lucko"
                    name = "Luck"
                    url = "https://lucko.me"
                    email = "git@lucko.me"
                }

                developer {
                    id = "zml"
                    name = "zml"
                    timezone = "America/Vancouver"
                }

                developer {
                    id = "Electroid"
                }
            }
        }
    }
}